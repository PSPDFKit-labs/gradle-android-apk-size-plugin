buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:1.3.1'
}

group = GROUP
version = VERSION_NAME

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
    distributionUrl = 'https://services.gradle.org/distributions/gradle-2.8-all.zip'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from 'build/docs/groovydoc'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives groovydocJar
    archives javadocJar
    archives sourcesJar
}

apply from: file('gradle/gradle-mvn-push.gradle')


apply plugin: 'maven-publish'

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}